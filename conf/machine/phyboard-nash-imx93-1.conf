#@TYPE: Machine
#@NAME: phyboard-nash-imx93-1
#@DESCRIPTION: PHYTEC phyBOARD-Nash i.MX93, 2 GB RAM, eMMC
#@ARTICLENUMBERS: PB-04729-001, PCL-077-23231211I

MACHINEOVERRIDES =. "mx93:phyboard-nash:"

include conf/machine/include/imx-base.inc
include conf/machine/include/phyimx93.inc

MACHINE_FEATURES += "camera screen can alsa emmc tpm2 nxpiw612-sdio"
MACHINE_FEATURES:remove:use-mainline-bsp = " nxpiw612-sdio"

KERNEL_DEVICETREE = " \
    freescale/imx93-phyboard-nash.dtb \
    freescale/imx93-phyboard-nash-jtag.dtbo \
    freescale/imx93-phyboard-nash-peb-av-10.dtbo \
    freescale/imx93-phyboard-nash-peb-wlbt-07.dtbo \
    freescale/imx93-phyboard-nash-pwm-fan.dtbo \
    freescale/imx93-phyboard-nash-vm016.dtbo \
    freescale/imx93-phycore-npu.dtbo \
    freescale/imx93-phycore-rpmsg.dtbo \
"
KERNEL_DEVICETREE:use-mainline-bsp = " \
    freescale/imx93-phyboard-nash.dtb \
    freescale/imx93-phyboard-nash-peb-wlbt-07.dtbo \
    freescale/imx93-phycore-rpmsg.dtbo \
"

IMX_DEFAULT_BOOTLOADER = "u-boot-phytec-imx"

IMAGE_BOOTLOADER = "imx-boot"

UBOOT_CONFIG_BASENAME = "imx93-phycore"
UBOOT_DTB_NAME = "${KERNEL_DEVICETREE_BASENAME}.dtb"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "${UBOOT_CONFIG_BASENAME}_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx93"

IMXBOOT_TARGETS = "flash_singleboot"

IMX_BOOT_SOC_TARGET = "iMX9"
IMX_BOOT_SEEK = "32"

# Set Serial console
SERIAL_CONSOLES = "115200;ttyLP0"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    lpddr4_dmem_1d_v202201.bin \
    lpddr4_dmem_2d_v202201.bin \
    lpddr4_imem_1d_v202201.bin \
    lpddr4_imem_2d_v202201.bin \
"
