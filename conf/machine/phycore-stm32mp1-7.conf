
#@TYPE: Machine
#@NAME: phycore-stm32mp1-7
#@DESCRIPTION: Configuration for phyCORE-STM32mp1-7 Sargas Dev Board, stm32mp153aac @650Mhz MPU, no 3D GPU, 512MB RAM, 128MB NAND 
#@BOOTSCHEME: SD-Card, NAND flash Trusted boot support (and Basic boot for SD-Card only)
#@ARTICLENUMBERS: PCM-068-310011I.A0/PCM-068-1534-1/PCM-939-1517-2

#@NEEDED_BSPLAYERS:

require conf/machine/include/phytec-machine-extlinux-config-phycore-stm32mp1-7.inc
include conf/machine/include/phytec-machine-common-stm32mp.inc

MACHINEOVERRIDES .= ":phycore"

EULA_FILE_ST_stm32mpcommon = "${PHYCORE_STM32MP_BASE}/conf/eula/${MACHINE}"
EULA_FILE_ST_MD5SUM_stm32mpcommon = "8b505090fb679839cefbcc784afe8ce9"

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa7thf-neon-vfpv4"
include conf/machine/include/tune-cortexa7.inc

# =========================================================================
# boot scheme
# =========================================================================
BOOTSCHEME_LABELS += "basic"
BOOTSCHEME_LABELS += "trusted"
#BOOTSCHEME_LABELS += "optee"


#PREFERRED_VERSION_u-boot-stm32mp = "2018.11"

# =========================================================================
# Machine settings
# =========================================================================
STM32MP_DT_FILES_PHYCORE += "phycore-stm32mp1-7"

# =========================================================================
# Machine features
# =========================================================================
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"
#MACHINE_FEATURES += "hdmi"
MACHINE_FEATURES += "nand"

# Splashscreen enabled
#MACHINE_FEATURES += "splashscreen"

# =========================================================================
# QT parameters
# =========================================================================
QT_QPA_PLATFORM = "linuxfb"

# =========================================================================
# Image
# =========================================================================
# Add ubi FSTYPES to default ones for nand volumes
IMAGE_FSTYPES += "stmultiubi"

# ST Naming rules partitions for UBI format are :
#   nand_<PageSize>_<BlockSize>
#   nor_<BlockSize>
# Like that a same UBI partition can be used for severals NAND/NOR providers

# UBI Args for NAND soldered by default on phyCORE-STM32mp1-7
# Micron MT29F1G08ABAEAWP
# LEB = BLOCK_SIZE - (2 * page size): 64*2048 - (2*2048)
MKUBIFS_ARGS_nand_2_64 = "--min-io-size 2048 --leb-size 126976 --max-leb-cnt 972 --space-fixup"
UBINIZE_ARGS_nand_2_64 = "--min-io-size 2048 --peb-size 128KiB"
EXTRA_UBIFS_SIZE_nand_2_64 = "2304"

# Define UBI labels to build
MULTIUBI_BUILD = "nand_2_64"

# Define image partition size (supposed to be set as max size in image recipe)
BOOTFS_PARTITION_SIZE = "12288"
# New value proposed for rootfs is 64MB
ROOTFS_PARTITION_SIZE = "65536"
# If we consider the highest constraint is NAND size (so < 128MB)
# Boot binaries 4 MB max (with optee) + bootfs 12 MB max + rootfs 64 MB max + userfs size (16MB) < 128 MB
USERFS_PARTITION_SIZE = "16384"
# New value proposed for vendorfs is 16MB
VENDORFS_PARTITION_SIZE = "16384"


# =========================================================================
# Kernel
# =========================================================================
# Kernel config
# Set this address to 0xC2000040, which is 0xC2000000 + 0x40.
# 0xC2000000 is the memory address where U-Boot will copy from flash the file uImage and 0x40 is uImage header size (64Bytes).
# With this value, U-Boot will be able to execute in place the zImage contained in uImage.
ST_KERNEL_LOADADDR   = "0xC2000040"

# For eval board: auto-load goodix module (touchscreen module)
#KERNEL_MODULE_AUTOLOAD = "goodix"

# Define the devicetree for Linux A7 examples
LINUX_A7_EXAMPLES_DT += "phycore-stm32mp1-7-a7-examples"


#PREFERRED_VERSION_linux-stm32mp = "4.19"

# =========================================================================
# flashlayout
# =========================================================================
# Define the config labels to use to generate flashlayout file
FLASHLAYOUT_CONFIG_LABELS += "nand-2-64"
FLASHLAYOUT_CONFIG_LABELS += "sdcard"

# =========================================================================
# M4 copro
# =========================================================================
# Define the devicetree for M4 example
CUBE_M4_EXAMPLES_DT += "phycore-stm32mp1-7-m4-examples"

# Define specific board reference to use
M4_BOARDS = "STM32MP157C-PHY"

# =========================================================================
# extlinux configuration
# =========================================================================
# As example, modify the default boot config for each target to M4 config
#UBOOT_EXTLINUX_DEFAULT_LABEL_mp1-7_sdcard       = "phycore-stm32mp1-7-m4-examples-sdcard"
#UBOOT_EXTLINUX_DEFAULT_LABEL_mp1-7_sdcard-optee = "phycore-stm32mp1-7-m4-examples-sdcard-optee"

# EDID firmware
HDMI_EDID_KERNEL_ARGS = "drm_kms_helper.edid_firmware=HDMI-A-1:edid/1280x720.bin"

# =========================================================================
# WIC for sdcard raw image
# =========================================================================
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"
WKS_FILE_DEPENDS ?= " \
    u-boot-stm32mp \
    tf-a-stm32mp \
    ${@bb.utils.contains('BOOTSCHEME_LABELS', 'optee', 'optee-os-stm32mp', '', d)} \
    st-image-bootfs \
    st-image-vendorfs \
    st-image-userfs \
"
# for generated a WIC file, please uncomment the 3 following lines
#IMAGE_FSTYPES += "wic"
#WKS_FILE_openstlinux-tiny += "${@bb.utils.contains('BOOTSCHEME_LABELS', 'optee', \
#                                'sdcard-tiny-phycore-stm32mp1-7-optee-1GB.wks', 'sdcard-tiny-phycore-stm32mp1-7-trusted-1GB.wks', d)}"

