#@TYPE: Machine
#@NAME: imx93-phyflex-libra-rdk-1
#@DESCRIPTION: PHYTEC phyFLEX-i.MX 93 Libra Rapid Development Kit, 1 or 2 GB RAM, eMMC
#@ARTICLENUMBERS: PD-05029-001a, PD-05029-002a, PFL-G-05-PT001, PFL-G-05-PT002

MACHINEOVERRIDES =. "mx93:libra:imx93-phyflex-libra-rdk:"

include conf/machine/include/imx-base.inc
include conf/machine/include/phyimx93.inc

MACHINE_FEATURES += "camera screen can alsa emmc tpm2 wifi bluetooth lwb5p"

KERNEL_DEVICETREE = " \
    freescale/imx93-phyflex-libra-rdk.dtb \
    freescale/imx93-phyflex-libra-rdk-peb-av-10.dtbo \
    freescale/imx93-phyflex-libra-rdk-lvds-ph128800t006-zhc01.dtbo \
    freescale/imx93-phyflex-libra-rdk-vm016.dtbo \
    freescale/imx93-phyflex-libra-rdk-vm020.dtbo \
"

IMX_DEFAULT_BOOTLOADER = "u-boot-phytec-imx"

IMAGE_BOOTLOADER = "imx-boot"

UBOOT_CONFIG_BASENAME = "imx93-phyflex"
UBOOT_DTB_NAME = "${KERNEL_DEVICETREE_BASENAME}.dtb"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "${UBOOT_CONFIG_BASENAME}_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx93"

IMXBOOT_TARGETS = "flash_singleboot"

IMX_BOOT_SOC_TARGET = "iMX9"
IMX_BOOT_SEEK = "32"

# Set Serial console
SERIAL_CONSOLES = "115200;ttyLP3"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    lpddr4_dmem_1d_v202201.bin \
    lpddr4_dmem_2d_v202201.bin \
    lpddr4_imem_1d_v202201.bin \
    lpddr4_imem_2d_v202201.bin \
"

# Set regdomain to ETSI (see lwb-if-regdomain.bb from meta-radio-summit)
LWB_REGDOMAIN ?= "ETSI"
