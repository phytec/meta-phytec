#@TYPE: Machine
#@NAME: imx95-libra-fpsc-1
#@DESCRIPTION: PHYTEC i.MX95 Libra FPSC, 8 GB RAM, eMMC, silicon revision A0
#@ARTICLENUMBERS: PCM-937-L-imx95.A2

MACHINEOVERRIDES =. "mx95:libra:imx95-libra-fpsc:"

# WORKAROUND to avoid parser error in imx-isp-phycam
# only i.MX95 is on 6.12.34 (imx-isp_%.3)
# other i.MX are on 6.12.20 (imx-isp_%.2)
#
# and non-applicable patch in libcamera
BBMASK:append = " \
    meta-phytec/dynamic-layers/fsl-bsp-release/recipes-bsp/isp-imx/ \
    meta-ampliphy/dynamic-layers/fsl-bsp-release/recipes-multimedia/libcamera/ \
"

include conf/machine/include/imx-base.inc
require conf/machine/include/phytec-machine-common.inc

# Common settings for PHYTEC's i.MX95 boards
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

MACHINE_FEATURES += "alsa bluetooth can caam emmc gpu pci screen serial wifi"

PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-master"
PREFERRED_PROVIDER_virtual/bootloader:use-nxp-bsp ?= "u-boot-phytec-imx"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-dev"
PREFERRED_PROVIDER_virtual/kernel:use-nxp-bsp ?= "linux-phytec-imx"
PREFERRED_PROVIDER_virtual/imx-system-manager ?= "imx-sm-phytec"
PREFERRED_PROVIDER_virtual/imx-oei ?= "imx-oei-phytec"

# Don't install kernelimage to rootfs
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""

# Wic
IMAGE_CLASSES += "wic-helper"
IMAGE_CLASSES:append:mx95-nxp-bsp = " wic-bootenv-helper"
WKS_FILES ?= "imx9-sdimage.wks"
WKS_BOOTIMAGESIZE ?= "64"

# MMC user area bootloader image offset
BOOTLOADER_SEEK ?= "32"
# eMMC boot partition bootloader image offset
BOOTLOADER_SEEK_EMMC ?= "0"
# Device information used by RAUC and some of its dependencies
EMMC_DEV ?= "0"

IMAGE_BOOT_FILES = "fitImage boot.scr.uimg"
IMAGE_BOOT_FILES:use-nxp-bsp = "fitImage bootenv.txt boot.scr.uimg"
IMAGE_BOOT_FILES:secureboot:mx95-nxp-bsp = "fitImage bootenv.txt"
IMAGE_BOOTLOADER = "imx-boot"

INITRAMFS_MAXSIZE = "262144"

# partup
IMAGE_CLASSES += "image-types-partup"
include partup-layout-config.inc

IMAGE_FSTYPES = "tar.gz wic.bmap wic.xz"

# Set Serial console
SERIAL_CONSOLES = "115200;ttyLP6"

# Set ATF platform name
ATF_PLATFORM = "imx95"
ATF_BOOT_UART_BASE = "0x42690000"

OEI_CONFIGS = "ddr tcm"
OEI_CORE = "m33"
OEI_SOC = "mx95"
OEI_BOARD = "mx95libra"

SYSTEM_MANAGER_CONFIG = "mx95libra"
SYSTEM_MANAGER_FIRMWARE_BASENAME = "m33_image"
SYSTEM_MANAGER_FIRMWARE_NAME = "${SYSTEM_MANAGER_FIRMWARE_BASENAME}"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    lpddr5_dmem_qb_v202409.bin \
    lpddr5_dmem_v202409.bin \
    lpddr5_imem_qb_v202409.bin \
    lpddr5_imem_v202409.bin \
"
DDR_TYPE   = "lpddr5"

SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_ARCH = "arm64"
# NXP bastardizes this variable to contain the U-Boot proper only. Set for mainline only.
UBOOT_BINARY:use-mainline-bsp = "flash.bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "${UBOOT_CONFIG_BASENAME}_defconfig,sdcard"
UBOOT_CONFIG_BASENAME = "imx95-libra-fpsc"
UBOOT_DTB_LOADADDRESS = "0xD9000000"
UBOOT_DTB_NAME = "imx95-libra-rdk-fpsc.dtb"
UBOOT_DTBO_LOADADDRESS = "0xD9100000"
UBOOT_ENTRYPOINT = "0xD0000000"
UBOOT_MAKE_TARGET = ""
UBOOT_PROVIDES_BOOT_CONTAINER = "1"
UBOOT_PROVIDES_BOOT_CONTAINER:use-nxp-bsp = "0"
UBOOT_SUFFIX = "bin"

IMX_BOOT_SOC_TARGET = "iMX95"
IMX_BOOT_SEEK = "32"
IMX_DEFAULT_BOOTLOADER = "u-boot-phytec-imx"
# NXP uses mx95-nxp-bsp override, so we have to use it, too (see imx-base-extend.inc)
IMX_EXTRA_FIRMWARE:mx95-nxp-bsp += "imx-oei-phytec"
IMX_EXTRA_FIRMWARE:mx95-nxp-bsp:remove = "imx-oei"
IMXBOOT_TARGETS = "flash_a55"

EXTERNAL_KERNEL_DEVICETREE ??= ""
KERNEL_DEVICETREE = " \
    freescale/imx95-libra-rdk-fpsc.dtb \
"
KERNEL_DEVICETREE:use-nxp-bsp = " \
    freescale/imx95-libra-rdk-fpsc.dtb \
    freescale/imx95-libra-rdk-fpsc-lvds-ph128800t006-zhc01.dtbo \
"
KERNEL_CLASSES += "kernel-fitimage"
KERNEL_IMAGETYPES += "fitImage"

# This variable needs to be set for imx-boot recipe from nxp, image does not get built into imx-boot
M4_DEFAULT_IMAGE_MX95 = "imx95-19x19-evk_m7_TCM_power_mode_switch.bin"
