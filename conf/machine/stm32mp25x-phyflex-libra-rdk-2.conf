#@TYPE: Machine
#@NAME: stm32mp25x-phyflex-libra-rdk-2
#@DESCRIPTION: phyFLEX-STM32MP25x Libra Rapid Development Kit (4 Gib RAM version)
#@BOOTSCHEME: SD-Card, eMMC (only OP-TEE boot support)
#@ARTICLENUMBERS: PD-05036-002A.A2

include conf/machine/include/phystm32mp.inc

# Define specific familly common machine name
MACHINEOVERRIDES .= ":stm32mp2common:stm32mp25common:stm32mp25x-libra"

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa35"
include conf/machine/include/arm/armv8a/tune-cortexa35.inc

# =========================================================================
# boot scheme
# =========================================================================
BOOTSCHEME_LABELS = "optee"

# =========================================================================
# Machine settings
# =========================================================================
# Kernel Device Tree
KERNEL_DEVICETREE = "st/stm32mp257f-phyflex-libra-rdk.dtb"

# Bootloaders Device Tree (same DTS name for TF-A, OP-TEE and U-boot)
BOOTLOADER_DT = "stm32mp257f-phyflex-fpsc-g-som-4Gb"

# =========================================================================
# Machine features
# =========================================================================
# Yocto standard machine features
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"

# PHYTEC expansion board machine feature (Hardware add-ons)
MACHINE_FEATURES += "phy-expansions"

# OpenSTLinux machine features
MACHINE_FEATURES:append:openstlinuxcommon = " splashscreen"
MACHINE_FEATURES:append:openstlinuxcommon = " watchdog"
# M33
MACHINE_FEATURES:append:openstlinuxcommon = " m33copro"
#GPU
MACHINE_FEATURES:append:openstlinuxcommon = " ${@'gpu' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES:append:openstlinuxcommon = " ${@'openvx' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES:append:openstlinuxcommon = " ${@'opencl' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES:append:openstlinuxcommon = " ${@'vulkan' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"

# =========================================================================
# Kernel
# =========================================================================

# Define the devicetree overlays enabled by default (when 'phy-expansions' machine feature enabled)
DEFAULT_PHY_EXPANSIONS = "stm32mp257f-phyflex-libra-rdk-lvds-ph128800t006-zhc01"
DEFAULT_PHY_EXPANSIONS += "stm32mp257f-phyflex-libra-rdk-usb3ss"

# =========================================================================
# flashlayout
# =========================================================================
# Define the config labels to use to generate flashlayout file
BOOTDEVICE_LABELS += "emmc"
BOOTDEVICE_LABELS += "sdcard"
